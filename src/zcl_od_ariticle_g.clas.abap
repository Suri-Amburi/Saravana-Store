CLASS ZCL_OD_ARITICLE_G DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC.

  PUBLIC SECTION.
*** STANDARD AMDP INTERFACE
    INTERFACES IF_AMDP_MARKER_HDB.
*** TYPES
    TYPES:
      BEGIN OF TY_ARITICLE,
        CLASS           TYPE KLAH-CLASS,
        MATNR           TYPE MARA-MATNR,
        MATKL           TYPE MARA-MATKL,
        SST_CODE        TYPE MARA-SATNR,
        PARENT          TYPE MARA-ZZARTICLE,
        PRICE_FROM      TYPE MARA-ZZPRICE_FRM,
        PRICE_TO        TYPE MARA-ZZPRICE_TO,
        SIZE1           TYPE MARA-SIZE1,
        BRAND_ID        TYPE MARA-BRAND_ID,
        ZZSTYLE         TYPE MARA-ZZSTYLE,
        MAKTX           TYPE MAKT-MAKTX,
        LIFNR           TYPE A502-LIFNR,
        KBETR           TYPE KONP-KBETR,
        NAME1           TYPE LFA1-NAME1,
        CITY            TYPE LFA1-ORT01,
        LAND1           TYPE LFA1-LAND1,
        ORT02           TYPE LFA1-ORT02,
        NAME2           TYPE LFA1-NAME2,
        PSTLZ           TYPE LFA1-PSTLZ,
        REGIO           TYPE LFA1-REGIO,
        STRAS           TYPE LFA1-STRAS,
        ADRNR           TYPE LFA1-ADRNR,
        ADDR2_STREET    TYPE LFA1-ADDR2_STREET,
        ADDR2_HOUSE_NUM TYPE LFA1-ADDR2_HOUSE_NUM,
        STCD3           TYPE STCD3,
        WGBEZ           TYPE WGBEZ,
      END OF TY_ARITICLE,
      TT_ARITICLE TYPE TABLE OF TY_ARITICLE.

    CLASS-METHODS :
      GET_ARTICLES
        IMPORTING VALUE(IQ_MARA)     TYPE STRING
                  VALUE(IQ_LFA1)     TYPE STRING
                  VALUE(IQ_GROUP_ID) TYPE STRING
        EXPORTING VALUE(T_ARTICLES)  TYPE TT_ARITICLE.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_OD_ARITICLE_G IMPLEMENTATION.
  METHOD : GET_ARTICLES BY DATABASE PROCEDURE
                         FOR HDB
                         LANGUAGE SQLSCRIPT
                         OPTIONS READ-ONLY
                         USING  KLAH KSSK MARA LFA1 A502 T023T KONP MAKT.

*** FILTERS
    LT_MARA     = APPLY_FILTER ( MARA, :IQ_MARA);
    LT_LFA1     = APPLY_FILTER ( LFA1, :IQ_LFA1);
    LT_GROUP_ID = APPLY_FILTER ( KLAH, :IQ_GROUP_ID);
*** GET GROUP HIERARCHY
    T_ARTICLES =
    SELECT KLAH1.CLASS,
           MARA.MATNR,
           MARA.MATKL,
           MARA.SATNR AS SST_CODE,
           MARA.ZZARTICLE AS PARENT,
           MARA.ZZPRICE_FRM AS PRICE_FROM,
           MARA.ZZPRICE_TO AS PRICE_TO ,
           MARA.SIZE1,
           MARA.BRAND_ID,
           MARA.ZZSTYLE,
           MAKT.MAKTX,
           A502.LIFNR,
           KONP.KBETR,
           LFA1.NAME1,
           LFA1.ORT01 AS CITY,
           LFA1.LAND1,
           LFA1.ORT02,
           LFA1.NAME2,
           LFA1.PSTLZ,
           LFA1.REGIO,
           LFA1.STRAS,
           LFA1.ADRNR,
           LFA1.ADDR2_STREET,
           LFA1.ADDR2_HOUSE_NUM,
           LFA1.STCD3,
           T023T.WGBEZ
           FROM KLAH AS KLAH
           LEFT OUTER JOIN :LT_MARA  AS MARA  ON KLAH.CLASS  = MARA.MATKL
           INNER JOIN MAKT  AS MAKT  ON MAKT.MATNR  = MARA.MATNR
           INNER JOIN KSSK  AS KSSK  ON KSSK.OBJEK  = KLAH.CLINT
           INNER JOIN :LT_GROUP_ID  AS KLAH1 ON KLAH1.CLINT = KSSK.CLINT
           INNER JOIN A502  AS A502  ON A502.MATNR  = MARA.MATNR
           INNER JOIN :LT_LFA1  AS LFA1  ON LFA1.LIFNR  = A502.LIFNR
           INNER JOIN KONP  AS KONP  ON KONP.KNUMH  = A502.KNUMH
           LEFT  OUTER JOIN T023T AS T023T ON T023T.MATKL = MARA.MATKL AND T023T.SPRAS = 'E'
           WHERE KLAH.KLART = '026' AND  KONP.LOEVM_KO = '';

ENDMETHOD.
    ENDCLASS.
